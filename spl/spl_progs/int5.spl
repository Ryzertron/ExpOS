alias currPID R9;
alias userSP R10;
alias return_addr R11;
alias process_table R12;


//Routine work

userSP = SP;
currPID = [SYSTEM_STATUS_TABLE+1];
process_table = PROCESS_TABLE + (currPID * 16);


[process_table + 13] = SP;

SP = [process_table + 11] * 512 - 1;

alias physicalPageNum R1;
alias offset R2;
alias sysCallNoPhysicalAddr R3;

alias sysCallNo R4;
sysCallNo=[([PTBR + 2 * ((userSP - 5)/ 512)] * 512) + (userSP - 5) % 512];

physicalPageNum = [PTBR + 2 * ((userSP - 1)/ 512)];
offset = (userSP - 1) % 512;

return_addr = (physicalPageNum * 512) + offset;

//Settig Mode Flag
[process_table + 9] = sysCallNo;



//--------------------------------------Open SysCall 2

if (sysCallNo==2)then 
    alias pprtBase R5;
    alias pprtIndex R6;
    breakpoint;
    //Finding free entry in PPRT
    pprtBase = [process_table + 11] * 512 + RESOURCE_TABLE_OFFSET;
    pprtIndex = 0;
    while (pprtIndex<8 && [pprtBase + 2*pprtIndex]!=-1)do
        pprtIndex = pprtIndex + 1;
    endwhile;

    //No free entries
    if (pprtIndex == 8)then

        [return_addr] = -3;

        [process_table + 9] = 0;

        SP = [process_table + 13];
        ireturn;
    endif;

    alias fileName R2;
    fileName = [([PTBR + 2 * ((userSP - 4)/ 512)] * 512) + (userSP - 4) % 512];
	
    //Calling Open SysCall
    multipush(R4,R5,R6,R9,R10,R11,R12);
    R1 = 3;
    R2 = fileName;
    breakpoint;
    call FILE_MANAGER;
    multipop(R4,R5,R6,R9,R10,R11,R12);

    //If Open SysCall Fails
    if ( R0 < 0 )then

        [return_addr] = R0;

        [process_table + 9] = 0;

        SP = [process_table + 13];
        ireturn;
    endif;

    [pprtBase + 2*pprtIndex] = FILE;
    [pprtBase + 2*pprtIndex + 1] = R0;


    [return_addr] = pprtIndex;

    [process_table + 9] = 0;

    SP = [process_table + 13];
    ireturn;

endif;

//-------------------------------------------Close SysCall 3
if (sysCallNo==3)then 
    alias fileDescriptor R5;
    alias pprtBase R6;
    alias indexOFT R7;
    
    pprtBase = [process_table + 11] * 512 + RESOURCE_TABLE_OFFSET;

    fileDescriptor = [([PTBR + 2 * ((userSP - 4)/ 512)] * 512) + (userSP - 4) % 512];

    if (fileDescriptor<0 || fileDescriptor>=8 || [pprtBase + 2*fileDescriptor]!=FILE )then
        //Return Value
        [return_addr] = -1;

        // Reset Mode Flag
        [process_table + 9] = 0;

        //Reset User Stack
        SP = [process_table + 13];
        ireturn;
    endif;

    indexOFT = [pprtBase + 2*fileDescriptor + 1];

    //Calling Close SysCall
    multipush(R5,R6,R11,R12);
    R1 = 4;
    R2 = indexOFT;
    call FILE_MANAGER;
    multipop(R5,R6,R11,R12);
    

    [pprtBase + 2*fileDescriptor] = -1;

    //Returning File Descriptor
    [return_addr] = 0;

    [process_table + 9] = 0;

    SP = [process_table + 13];

    ireturn;

endif;

//--------------------------------------Seek SysCall 6
if (sysCallNo == 6)then
    alias fileDescriptor R5;
    alias pprtBase R6;
    alias OFTIndex R7;
    alias inodeIndex R2;
    
    pprtBase = [process_table + 11] * 512 + RESOURCE_TABLE_OFFSET;

    //Retrieving fileDescriptor
    fileDescriptor = [([PTBR + 2 * ((userSP - 4)/ 512)] * 512) + (userSP - 4) % 512];

    //File Descriptor Invalid
    if (fileDescriptor<0 || fileDescriptor>=8 || [pprtBase + 2*fileDescriptor]!=FILE  )then
        //Return Value
        [return_addr] = -1;

        // Reset Mode Flag
        [process_table + 9] = 0;

        //Reset User Stack
        SP = [process_table + 13];
        ireturn;
    endif;

    OFTIndex = [pprtBase + 2*fileDescriptor + 1];

    inodeIndex = [ OPEN_FILE_TABLE + 4*OFTIndex ];

    //Calling Acquire Inode
    multipush(R2,R4,R5,R6,R7,R9,R10,R11,R12);
    R1 = 4;
    R2 = inodeIndex;
    R3 = currPID;
    call RESOURCE_MANAGER;
    multipop(R2,R4,R5,R6,R7,R9,R10,R11,R12);


    //If Acquire Inode Fails
    if (R0 == -1)then
        [return_addr] = -1;
        [process_table + 9] = 0;
        SP = [process_table + 13];
        ireturn;
    endif;

    alias lSeekAddr R8;
    lSeekAddr = OPEN_FILE_TABLE + 4*OFTIndex + 2;

    alias Offset R13;
    //Retrieving Offset
    Offset = [([PTBR + 2 * ((userSP - 3)/ 512)] * 512) + (userSP - 3) % 512];

    alias fileSize R3;
    if (inodeIndex == INODE_ROOT)then
        fileSize = 480;
    else
        fileSize=[INODE_TABLE + 16*inodeIndex + 2];
    endif;

    // If seek goes to negative 
    if ( [lSeekAddr] + Offset < 0 )then

        multipush(R11,R12);
        R1 = 5;
        R2 = inodeIndex;
        R3 = currPID;
        call RESOURCE_MANAGER;
        multipop(R11,R12);

        [return_addr] = -2;
        [process_table + 9] = 0;
        SP = [process_table + 13];
        ireturn;
    endif;

    // Reset lSeek if Offset is 0
    if ( Offset == 0 )then
        [lSeekAddr] = 0;
    else 

    // If lSeek is greater than file size than set to file size
        if ( [lSeekAddr] + Offset > fileSize )then
            [lSeekAddr] = fileSize;
        else

    //Otherwise
            [lSeekAddr] = [lSeekAddr] + Offset;
        endif;
    endif;

    multipush(R11,R12);
    R1 = 5;
    R2 = inodeIndex;
    R3 = currPID;
    call RESOURCE_MANAGER;
    multipop(R11,R12);

    [return_addr] = 0;
    [process_table + 9] = 0;
    SP = [process_table + 13];
    ireturn;

endif;
