alias userSP R0;
userSP=SP;
alias cpid R10;
R10 = [SYSTEM_STATUS_TABLE + 1];

//UPTR set to Stack Ptr
[PROCESS_TABLE + ( cpid * 16) + 13] = SP;

//Stack Pointer set to bottom UA Page No.
SP = [PROCESS_TABLE + (cpid * 16) + 11] * 512 - 1;

//Set MODE Flag to 9
[PROCESS_TABLE + cpid * 16 + 9] = 9;

alias physicalPageNum R1;
alias offset R2;
alias fileNamePhysicalAddr R3;

//find fileName from ARGS

physicalPageNum = [PTBR + 2 * ((userSP - 4)/ 512)];
offset = (userSP - 4) % 512;
fileNamePhysicalAddr = (physicalPageNum * 512) + offset;
alias fileName R4;
fileName=[fileNamePhysicalAddr];

//File Existence Checks

alias fileIndex R5;
fileIndex = 0;

while(fileIndex < MAX_FILE_NUM )do
    if ( [ INODE_TABLE + ( fileIndex * 16 ) + 1 ] == fileName )then
        break;
    endif;
    fileIndex = fileIndex + 1;
endwhile;

//Case 1 File Not Found or File not having EXEC permissions
//If no such file exists or XEXE format is not applicable
if ( fileIndex == MAX_FILE_NUM || [ INODE_TABLE + ( fileIndex * 16 ) + 0 ] != EXEC )then
    alias physicalAddrRetVal R6;
    physicalAddrRetVal = ([PTBR + 2 * ((userSP - 1) / 512)] * 512) + ((userSP - 1) % 512);
    [physicalAddrRetVal] = -1;

    // Reset Mode Flag
    [PROCESS_TABLE + cpid * 16 + 9] = 0;

    //Reset Stack pointer
    SP = [PROCESS_TABLE + ( cpid * 16) + 13];
    ireturn;
endif;

//Condition Valid

//CAll EXIT From MOD1
multipush(R0,R1,R2,R3,R4,R5,R6,R10,PTBR,PTLR);

R1 = 3;
R2 = cpid;
call PROCESS_MANAGER;

multipop(R0,R1,R2,R3,R4,R5,R6,R10,PTBR,PTLR);

//Reclaiming User Area without invoking MOD2

alias UAPage R7;
UAPage = [PROCESS_TABLE + (cpid * 16) + 11];
[MEMORY_FREE_LIST + UAPage] = [MEMORY_FREE_LIST + UAPage] + 1;
[SYSTEM_STATUS_TABLE + 2] = [SYSTEM_STATUS_TABLE + 2] - 1;

//UA set to bottom without any offset
SP = [PROCESS_TABLE + (cpid * 16) + 11] * 512 - 1;


//Initializing New Per Process Resource Table
alias PPRE R8 ;
PPRE = (UAPage*512) + RESOURCE_TABLE_OFFSET;
while ( PPRE < (UAPage+1)*512 )do
    [PPRE] = -1;
    PPRE = PPRE + 2;
endwhile;

//Setting State of the Process to RUNNING
[PROCESS_TABLE + (cpid * 16) + 4] = RUNNING;

//Set Inode index Entry in PCB (7)
[PROCESS_TABLE + (cpid * 16) + 7] = fileIndex;

PTBR = [PROCESS_TABLE + (cpid * 16) + 14];
PTLR = [PROCESS_TABLE + (cpid * 16) + 15];

//Library
[PTBR+0] = 63;
[PTBR+1] = "0100";
[PTBR+2] = 64;
[PTBR+3] = "0100";

//Heap
[PTBR+4] = -1;
[PTBR+5] = "0000";
[PTBR+6] = -1;
[PTBR+7] = "0000";

//Code

//Only the First Code block is assigned
multipush(R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,PTBR,PTLR);
R1 = 5;
R2 = [ INODE_TABLE + ( fileIndex * 16 ) + 8 ];
R3 = cpid;
call MEMORY_MANAGER;

[ PTBR + 8 ] = R0;
[ PTBR + 9 ] = "0100";

multipop(R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,PTBR,PTLR);

[PTBR+10] = -1;
[PTBR+11] = "0000";
[PTBR+12] = -1;
[PTBR+13] = "0000";
[PTBR+14] = -1;
[PTBR+15] = "0000";

//Stack
multipush(R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,PTBR,PTLR);
R1 = 1;
call MEMORY_MANAGER;
[PTBR + 16 ] = R0;
multipop(R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,PTBR,PTLR);
[PTBR + 17 ] = "0110";

multipush(R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,PTBR,PTLR);
R1 = 1;
call MEMORY_MANAGER;
[PTBR + 18 ] = R0;
multipop(R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,PTBR,PTLR);
[PTBR + 19 ] = "0110";

//Initialising Disk Map Table
alias index R9;
index=0;
while(index < 10)do
    [ DISK_MAP_TABLE + cpid*10 + index] = - 1;
    index = index + 1;
endwhile;

[ DISK_MAP_TABLE + cpid*10 + 4] =  [ INODE_TABLE + ( fileIndex * 16 ) + 8 ];
[ DISK_MAP_TABLE + cpid*10 + 5] =  [ INODE_TABLE + ( fileIndex * 16 ) + 9 ];
[ DISK_MAP_TABLE + cpid*10 + 6] =  [ INODE_TABLE + ( fileIndex * 16 ) + 10 ];
[ DISK_MAP_TABLE + cpid*10 + 7] =  [ INODE_TABLE + ( fileIndex * 16 ) + 11 ];

[[PTBR + 16] * 512] = [[PTBR + 8] * 512 + 1];


// Reset Mode Flag
[PROCESS_TABLE + cpid * 16 + 9] = 0;

SP = 8 * 512;
ireturn;
